!#  File name : real3d-th.f90	!# Last modified : 14 Mar 2016
!#  Fortran program for Gross-Pitaevskii equation in three-dimensional 
!#  anisotropic trap by real time propagation (Fortran 90/95 Version)
!#
!# BEC-GP-OMP-FOR programs are developed by:
!#
!# Luis E. Young-S.
!# (Departamento de Ciencias Basicas, Universidad Santo Tomas, Tunja, Boyaca, Colombia)
!#
!# Paulsamy Muruganandam
!# (Department of Physics, Bharathidasan University, Tiruchirappalli, Tamil Nadu, India)
!#
!# Sadhan K. Adhikari
!# (Instituto de Fisica Teorica, UNESP - Sao Paulo State University, Brazil)
!#
!# Vladimir Loncar, Dusan Vudragovic, Antun Balaz
!# (Scientific Computing Laboratory, Center for the Study of Complex Systems,
!# Institute of Physics Belgrade, Serbia)
!#
!#
!# Public use and modification of these codes are allowed provided that the following
!# papers are cited:
!# [1] L. E. Young-S. et al., Comput. Phys. Commun. 220 (2017) 503.
!# [2] P. Muruganandam and S. K. Adhikari, Comput. Phys. Commun. 180 (2009) 1888.
!# [3] D. Vudragovic et al., Comput. Phys. Commun. 183 (2012) 2021.
!# [4] R. Kishor Kumar et al., Comput. Phys. Commun. 195 (2015) 117.
!# [5] B. Sataric et al., Comput. Phys. Commun. 200 (2016) 411.
!# [6] V. Loncar et al., Comput. Phys. Commun. 200 (2016) 406.
!# [7] L. E. Young-S. et al., Comput. Phys. Commun. 204 (2016) 209.
!# [8] V. Loncar et al., Comput. Phys. Commun. 209 (2016) 190.
!#
!# The authors would be grateful for all information and/or comments
!# regarding the use of the programs.
!
!# To compile :
!# (1) Intel Fortran Compiler (newer versions)
! ifort -O3 -qopenmp -w -mcmodel medium -shared-intel -V
!#  (note that some older versions require -openmp instead of -qopenmp)
!!
!# (2) GNU Fortran (gfortran)
! gfortran -O3  -fopenmp  -w  

MODULE COMM_DATA
! NX, NY, NZ : Number of space mesh points (X, Y and Z)  
  INTEGER, PARAMETER :: NX = 200, NXX = NX-1, NX2 = NX/2
  INTEGER, PARAMETER :: NY = 160, NYY = NY-1, NY2 = NY/2
  INTEGER, PARAMETER :: NZ = 120, NZZ = NZ-1, NZ2 = NZ/2
  INTEGER, PARAMETER :: NNN = NX*NY*NZ
! NSTP : Number of iterations to introduce the nonlinearity. 
! NSTP=0 reads the wave function, /=0 calculates the wave function.
! NPAS : Number of subsequent iterations with fixed nonlinearity.
! NRUN : Number of final time iterations with fixed nonlinearity. 
  INTEGER, PARAMETER :: NSTP = 40000, NPAS = 100, NRUN = 4000
  INTEGER, PARAMETER :: NUMBER_OF_THREADS = 0 ! sets the number of CPU cores to be used
     !     NUMBER_OF_THREADS = 0 deactivates the command and uses all available CPU cores   
  REAL (8), PARAMETER :: PI = 3.14159265358979D0
END MODULE COMM_DATA
!
MODULE GPE_DATA
  USE COMM_DATA, ONLY : NX, NY, NZ, PI
  REAL (8), PARAMETER :: AHO = 1.D-6     		 ! Unit of length (= 1 MICRON)            
  REAL (8), PARAMETER :: Bohr_a0 =  5.2917720859D-11/AHO ! Bohr radius (scaled with AHO)
  COMPLEX (8), PARAMETER :: CI = (0.D0,1.D0)		 ! Complex i
!  
  REAL (8), PARAMETER :: DX = 0.1D0, DY = DX, DZ = DX	 ! DX, DY, DZ : SPACE STEPS
  REAL (8), PARAMETER :: DT = 0.002D0			 ! DT : TIME STEP 
 
  INTEGER, PARAMETER :: NATOMS = 500			 ! Number of Atoms
  REAL (8), PARAMETER :: AS = 67.532483D0*Bohr_a0	 ! Scattering length (in units of Bohr_a0)
  REAL (8), PARAMETER :: GAMMA = 1.D0, ANU = 1.414214D0, LAMBDA = 2.D0	! NU, ANU, LAMBDA : Parameteres of Trap in x, y and z directions
! 
  REAL (8), PARAMETER :: G0 = 4.D0*PI*AS*NATOMS! 22.454D0 !		! Three-dim nonlinearity 
  REAL (8), PARAMETER :: GPAR = 0.5D0					! Change for dynamics
!
! OPTION   decides which equation to be solved.
! OPTION=1 Solves -psi_xx-psi_yy-psi_zz+V(x,y,z)psi+G0|psi|^2 psi=i psi_t
! OPTION=2 Solves [-psi_xx-psi_yy-psi_zz+V(x,y,z)psi]/2+G0|psi|^2 psi=i psi_t
  INTEGER, PARAMETER :: OPTION = 2 
! X(0:NX), Y(0:NY), Z(0:NZ) : Space mesh, V(0:NX,0:NY,0:NZ) : Potential, 
! CP(0:NX,0:NY,0:NZ) : Wave function  
  REAL (8), DIMENSION(:), ALLOCATABLE :: X, X2, Y, Y2, Z, Z2
  REAL (8), DIMENSION(:,:,:), ALLOCATABLE :: V,  R2
  COMPLEX (8), DIMENSION(:,:,:), ALLOCATABLE :: CP
!  REAL (8), DIMENSION(0:NX) :: X, X2
!  REAL (8), DIMENSION(0:NY) :: Y, Y2
!  REAL (8), DIMENSION(0:NZ) :: Z, Z2
!  REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: V, R2
!  COMPLEX (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: CP 
  REAL (8) :: G,  XOP
  INTEGER :: NO_OF_THREADS 
END MODULE GPE_DATA

MODULE CN_DATA
  USE COMM_DATA, ONLY : NX, NY, NZ
 COMPLEX (8), DIMENSION(:), ALLOCATABLE :: CALA, CGAA, CALB, CGAB, CALC, CGAC, CBE
!  COMPLEX (8), DIMENSION(0:NX) :: CALA, CGAA
!  COMPLEX (8), DIMENSION(0:NY) :: CALB, CGAB
 ! COMPLEX (8), DIMENSION(0:NZ) :: CALC, CGAC
  COMPLEX (8) :: CT0X, CT0Y, CT0Z
  COMPLEX (8) :: CA0, CB0, CC0, CA0R, CB0R, CC0R
END MODULE CN_DATA 

PROGRAM GROSS_PITAEVSKII_SSCN_3D
  USE OMP_LIB
  USE COMM_DATA !, ONLY : NX, NY, NZ, NX2, NY2, NZ2, NSTP, NPAS, NRUN,NUMBER_OF_THREADS
  USE GPE_DATA
  IMPLICIT NONE
! Subroutine INITIALIZE() used to initialize the space mesh X(I) and the initial wave function. 
! Subroutine CALCULATE_TRAP() used to initialize the harmonic oscillator potential V. 
! Subroutine COEF() used to generate the coefficients for the Crank-Nicholson Scheme. 
! The routine CALCNU() performs time progation for the non-derivative part and LU() performs
! time propagation of derivative part. NORM() calculates the norm and 
! normalizes the wave function, CHEM() and RAD() are used to calculate the 
! chemical potential, energy and the rms radius, respectively. The function 
! DIFF() used to calculate the space derivatives of the wave function used 
! in CHEM() and SIMP() does the integration by Simpson's rule.
!------------------------ interface blocks -----------------------       
  INTERFACE 
    SUBROUTINE ALLOCATE_VARIABLES()
    END SUBROUTINE ALLOCATE_VARIABLES
  END INTERFACE
!
  INTERFACE 
    SUBROUTINE FREE_VARIABLES()
    END SUBROUTINE FREE_VARIABLES
  END INTERFACE
! 
 INTERFACE 
    SUBROUTINE INITIALIZE()
    END SUBROUTINE INITIALIZE
  END INTERFACE
!------------------------
  INTERFACE 
    SUBROUTINE CALCULATE_TRAP()
    END SUBROUTINE CALCULATE_TRAP
  END INTERFACE
!------------------------
  INTERFACE 
    SUBROUTINE COEF()
    END SUBROUTINE COEF
  END INTERFACE
!------------------------
  INTERFACE 
    SUBROUTINE CALCNU()
!      COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(INOUT) :: CP
 !     REAL (8), INTENT(IN) :: DT
    END SUBROUTINE CALCNU
  END INTERFACE
!------------------------
  INTERFACE 
    SUBROUTINE LUX(CP)
      COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(INOUT) :: CP
    END SUBROUTINE LUX
  END INTERFACE
!------------------------
  INTERFACE 
    SUBROUTINE LUY(CP)
      COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(INOUT) :: CP
    END SUBROUTINE LUY
  END INTERFACE
!------------------------ 
  INTERFACE 
    SUBROUTINE LUZ(CP)
      COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(INOUT) :: CP
    END SUBROUTINE LUZ
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE NORM(CP, ZNORM)
      COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(INOUT) :: CP
      REAL (8), INTENT(OUT) :: ZNORM
    END  SUBROUTINE NORM
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE RAD(CP2, RMS)
      REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: CP2
      REAL (8), DIMENSION(:), INTENT(OUT) :: RMS
    END  SUBROUTINE RAD
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE CHEM(CP, MU, EN)
      COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: CP
      REAL (8), INTENT(OUT) :: MU, EN
    END  SUBROUTINE CHEM
  END INTERFACE
!---------------------------
  INTERFACE
    SUBROUTINE WRITE_3D(FUNIT, U2)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
    END  SUBROUTINE WRITE_3D
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE WRITE_2DXY(FUNIT, U2)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
    END  SUBROUTINE WRITE_2DXY
  END INTERFACE
!----------------------------
  INTERFACE
    SUBROUTINE WRITE_2DXZ(FUNIT, U2)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
    END  SUBROUTINE WRITE_2DXZ
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE DEN2DXZ(FUNIT, U2, DE2DXZ)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
      REAL (8), DIMENSION(0:,0:), INTENT(OUT) :: DE2DXZ
    END  SUBROUTINE DEN2DXZ
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE DEN2DXY(FUNIT, U2, DE2DXY)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
      REAL (8), DIMENSION(0:,0:), INTENT(OUT) :: DE2DXY
    END  SUBROUTINE DEN2DXY
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE DEN1DZ(FUNIT, U2, DE1DZ)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
      REAL (8), DIMENSION(0:), INTENT(OUT) :: DE1DZ
    END  SUBROUTINE DEN1DZ
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE DEN1DX(FUNIT, U2, DE1DX)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
      REAL (8), DIMENSION(0:), INTENT(OUT) :: DE1DX
    END  SUBROUTINE DEN1DX
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE DEN1DY(FUNIT, U2, DE1DY)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
      REAL (8), DIMENSION(0:), INTENT(OUT) :: DE1DY
    END  SUBROUTINE DEN1DY
  END INTERFACE
!------------------------ 
  INTERFACE
    SUBROUTINE DEN2DYZ(FUNIT, U2, DE2DYZ)
      IMPLICIT NONE
      INTEGER :: FUNIT
      REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: U2
      REAL (8), DIMENSION(0:,0:), INTENT(OUT) :: DE2DYZ
    END  SUBROUTINE DEN2DYZ
  END INTERFACE
!-------------------- END INTERFACE BLOCKS -----------------------
  INTEGER :: K,I,J,KK 
  REAL (8), DIMENSION(:,:,:), ALLOCATABLE :: CP2
!  REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: CP2
!  <r> = RMS(1), <x> = RMS(2), <y> = RMS(3), <z> = RMS(4)
  REAL (8), DIMENSION(4) :: RMS
  REAL (8) :: GSTP 
  REAL (8) :: ZNORM, MU, EN, T, T1, T2, TMP
  REAL (8), DIMENSION(0:NZ) :: DEN1Z
  REAL (8), DIMENSION(0:NX, 0:NY) :: DEN2XY
  REAL (8), DIMENSION(0:NX) :: DEN1X
  REAL (8), DIMENSION(0:NX, 0:NZ) :: DEN2XZ
  REAL (8), DIMENSION(0:NY, 0:NZ) :: DEN2YZ
  REAL (8), DIMENSION(0:NY) :: DEN1Y
  INTEGER :: CLCK_COUNTS_BEG, CLCK_COUNTS_END, CLCK_RATE 
!
  CALL SYSTEM_CLOCK ( CLCK_COUNTS_BEG, CLCK_RATE )
  CALL CPU_TIME (T1)
!
   IF(NUMBER_OF_THREADS.EQ.0) GO TO 9000
  
  CALL OMP_SET_NUM_THREADS(NUMBER_OF_THREADS)

  9000 CONTINUE

!  print *, omp_get_max_threads()
  !$OMP PARALLEL
    !$OMP MASTER
      NO_OF_THREADS = OMP_GET_NUM_THREADS()
    !$OMP END MASTER
  !$OMP END PARALLEL
!


  SELECT CASE (OPTION)
    CASE (1)
      XOP = 1.D0
    CASE (2)
      XOP = 2.D0
    CASE (:0,3:)
      PRINT *, 'ERROR: Wrong OPTION', OPTION
      STOP
  END SELECT
!
  

 CALL ALLOCATE_VARIABLES()
  ALLOCATE(CP2(0:NX, 0:NY, 0:NZ))


  OPEN(17, FILE = 'real3d-th-out.txt')
  OPEN(14, FILE = 'real3d-th-rms.txt')
 Write(17,*)  'Number of Threads =',     NO_OF_THREADS
!
  WRITE(17,900) OPTION 
  WRITE(14,900) OPTION 
  WRITE(17,*) 
  WRITE(14,*) 
  WRITE(17,901) NATOMS, AHO
  WRITE(17,902) AS/Bohr_a0 
  WRITE(17,903) G0 
  WRITE(17,904) GAMMA, ANU, LAMBDA
  WRITE(17,*)
  WRITE(17,905) NX, NY, NZ
  WRITE(17,906) DX, DY, DZ
  WRITE(17,907) NSTP, NPAS, NRUN  
  WRITE(17,908) DT 
  WRITE(17,*)
  WRITE(17, 1014) GPAR 
  WRITE(17,*)
  900 FORMAT(' Real time propagation 3D,   OPTION =',I3 )
  901 FORMAT('  Number of Atoms N =',I10,', Unit of length AHO =',F12.8,' m')
  902 FORMAT('  Scattering length a = ',F9.2,'*a0 ')
  903 FORMAT('  Nonlinearity G_3D =',F16.6)
  904 FORMAT('  Parameters of trap: NU =',F9.6, ', ANU =',F9.6, ', LAMBDA =',F9.6)
  905 FORMAT(' # Space Stp: NX = ', I8, ', NY = ', I8, ', NZ = ', I8)
  906 FORMAT('  Space Step: DX = ', F10.6, ', DY = ', F10.6, ', DZ = ', F10.6)
  907 FORMAT(' # Time Stp : NSTP = ',I9,', NPAS = ',I9,', NRUN = ',I9)
  908 FORMAT('   Time Step:   DT = ', F10.6)
  1014 FORMAT(' * Change for dynamics: GPAR = ',F11.3 ' *')
! INITIALIZE() initializes the starting normalized wave function 'CP'.
  CALL INITIALIZE()
! CALCULATE_TRAP() initializes the harmonic oscillator potential V.
  CALL CALCULATE_TRAP()
! COEF() defines the coefficients of Crank-Nicholson Scheme.
  CALL COEF()
!
  WRITE (17, 1001)
  WRITE (17, 1002)
  WRITE (17, 1001)
  WRITE (14, 1001)
  WRITE (14, 1012)
  WRITE (14, 1001)
  1001 FORMAT (19X,'-----------------------------------------------------')
  1002 FORMAT (20X, 'Norm', 6X, 'Chem', 8X, 'Ener/N', 5X, '<r>', 5X, '|Psi(0,0,0)|^2')
  1012 FORMAT ('Values of rms size:', 5X, '<r>', 10X, '<x>', 10X, '<y>', 10X, '<z>')
  1003 FORMAT ('Initial : ', 4X, F11.4, 2F12.3, 2F11.3)
  1013 FORMAT (11X, 'Initial:', F12.5, 3F13.5)
!
    !$OMP PARALLEL DO PRIVATE(I, J, KK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
  !OPEN(31, FILE = 'real3d-th-den-init.txt')
  !CALL WRITE_3D(31, CP2)
  !CLOSE(31)

  !OPEN(11, FILE = 'real3d-th-th-den-init1d_z.txt')
  !OPEN(12, FILE = 'real3d-th-den-init1d_x.txt')
  !OPEN(13, FILE = 'real3d-th-den-init1d_y.txt')
  !CALL DEN1DZ(11, CP2, DEN1Z)
  !CALL DEN1DX(12, CP2, DEN1X)
  !CALL DEN1DY(13, CP2, DEN1Y)
  !CLOSE(11)
  !CLOSE(12)
  !CLOSE(13)

  !OPEN(21, FILE = 'real3d-th-den-init2d_xz.txt')
  !OPEN(22, FILE = 'real3d-th-den-init2d_xy.txt')
  !OPEN(23, FILE = 'real3d-th-den-init2d_yz.txt')
  !CALL DEN2DXZ(21, CP2, DEN2XZ)
  !CALL DEN2DXY(22, CP2, DEN2XY)
  !CALL DEN2DYZ(23, CP2, DEN2YZ)
  !CLOSE(21)
  !CLOSE(22)
  !CLOSE(23)

  !OPEN(32, FILE = 'real3d-th-den-init3d_x0z.txt')
  !OPEN(33, FILE = 'real3d-th-den-init3d_xy0.txt')
  !CALL WRITE_2DXZ(32, CP2)
  !CALL WRITE_2DXY(33, CP2)
  !CLOSE(32)
  !CLOSE(33)
!   
  IF(NSTP /= 0 ) THEN
    GSTP = XOP*G0/DFLOAT(NSTP)    
    G = 0.D0
    CALL NORM(CP, ZNORM)	! NORM() calculates norm and restores normalization.
    CALL CHEM(CP, MU, EN)	! CHEM() calculates the chemical potential MU and energy EN.
     !$OMP PARALLEL DO PRIVATE(I, J, KK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
    CALL RAD(CP2, RMS)		! RAD() calculates the r.m.s radius RMS.
    WRITE (17, 1003) ZNORM, MU/XOP, EN/XOP, RMS(1), CP2(NX2, NY2, NZ2)
    WRITE (14, 1013) RMS(1:4) 
! 
    DO K = 1, NSTP		! NSTP iterations to introduce the nonlinearity
      G = G + GSTP
      CALL CALCNU()
      CALL LUX(CP)
      CALL LUY(CP)
      CALL LUZ(CP)
    END DO
    CALL NORM(CP, ZNORM)
    CALL CHEM(CP, MU, EN)
      !$OMP PARALLEL DO PRIVATE(I, J, KK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
    CALL RAD(CP2, RMS)
    WRITE (17, 1005) ZNORM, MU/XOP, EN/XOP, RMS(1), CP2(NX2, NY2, NZ2)
    WRITE (14, 1015) RMS(1:4)
    1005 FORMAT('After NSTP iter.:', F8.4, 2F12.3, 2F11.3)
    1015 FORMAT (2X, 'After NSTP iter.:', F12.5, 3F13.5)  
!
    !OPEN(31, FILE = 'real3d-th-den-nstp.txt')
    !CALL WRITE_3D(31, CP2)
    !CLOSE(31)

    !OPEN(11, FILE = 'real3d-th-den-nstp1d_z.txt')
    !OPEN(12, FILE = 'real3d-th-den-nstp1d_x.txt')
    !OPEN(13, FILE = 'real3d-th-den-nstp1d_y.txt')
    !CALL DEN1DZ(11, CP2, DEN1Z)
    !CALL DEN1DX(12, CP2, DEN1X)
    !CALL DEN1DY(13, CP2, DEN1Y)
    !CLOSE(11)
    !CLOSE(12)
    !CLOSE(13)

    !OPEN(21, FILE = 'real3d-th-den-nstp2d_xz.txt')
    !OPEN(22, FILE = 'real3d-th-den-nstp2d_xy.txt')
    !OPEN(23, FILE = 'real3d-th-den-nstp2d_yz.txt')
    !CALL DEN2DXZ(21, CP2, DEN2XZ)
    !CALL DEN2DXY(22, CP2, DEN2XY)
    !CALL DEN2DYZ(23, CP2, DEN2YZ)
    !CLOSE(21)
    !CLOSE(22)
    !CLOSE(23)

    !OPEN(32, FILE = 'real3d-th-den-nstp3d_x0z.txt')
    !OPEN(33, FILE = 'real3d-th-den-nstp3d_xy0.txt')
    !CALL WRITE_2DXZ(32, CP2)
    !CALL WRITE_2DXY(33, CP2)
    !CLOSE(32)
    !CLOSE(33)
!   
  ELSE
    G = XOP*G0
    CALL NORM(CP, ZNORM)
    CALL CHEM(CP, MU, EN) 
      !$OMP PARALLEL DO PRIVATE(I, J, KK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
    CALL RAD(CP2, RMS)  
    WRITE (17, 1003) ZNORM, MU/XOP, EN/XOP, RMS(1), CP2(NX2, NY2, NZ2)
    WRITE (14, 1013) RMS(1:4) 
  END IF
!
  T = 0.D0
  IF((NPAS /= 0).OR.(NRUN /= 0)) OPEN(8, FILE = 'real3d-th-dyna.txt')  
!
  DO K = 1, NPAS ! NPAS iterations transient
     T = T + DT
     CALL CALCNU()
     CALL LUX(CP)
     CALL LUY(CP)
     CALL LUZ(CP)
      IF (MOD(K,2).EQ.0) THEN
       !$OMP PARALLEL DO PRIVATE(I, J, KK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
       CALL RAD(CP2, RMS)
       WRITE(8,1008) T*XOP,  RMS(2), RMS(3), RMS(4)
      END IF
  END DO
  IF(NPAS /= 0)THEN   
    CALL NORM(CP, ZNORM)
    CALL CHEM(CP, MU, EN)
      !$OMP PARALLEL DO PRIVATE(I, J, Kk, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
    CALL RAD(CP2, RMS)
    WRITE (17, 1006) ZNORM, MU/XOP, EN/XOP, RMS(1), CP2(NX2, NY2, NZ2)
    WRITE (14, 1016) RMS(1:4)
    1006 FORMAT('After NPAS iter.:',F8.4, 2F12.3, 2F11.3)
    1016 FORMAT (2X, 'After NPAS iter.:', F12.5, 3F13.5)
  END IF  
! 
   OPEN(31, FILE = 'real3d-th-den.txt')
    CALL WRITE_3D(31, CP2)
    CLOSE(31)

    OPEN(11, FILE = 'real3d-th-den1d_z.txt')
    OPEN(12, FILE = 'real3d-th-den1d_x.txt')
    OPEN(13, FILE = 'real3d-th-den1d_y.txt')
    CALL DEN1DZ(11, CP2, DEN1Z)
    CALL DEN1DX(12, CP2, DEN1X)
    CALL DEN1DY(13, CP2, DEN1Y)
    CLOSE(11)
    CLOSE(12)
    CLOSE(13)

    !OPEN(21, FILE = 'real3d-th-den2d_xz.txt')
    !OPEN(22, FILE = 'real3d-th-den2d_xy.txt')
    !OPEN(23, FILE = 'real3d-th-den2d_yz.txt')
    !CALL DEN2DXZ(21, CP2, DEN2XZ)
    !CALL DEN2DXY(22, CP2, DEN2XY)
    !CALL DEN2DYZ(23, CP2, DEN2YZ)
    !CLOSE(21)
    !CLOSE(22)
    !CLOSE(23)

    !OPEN(32, FILE = 'real3d-th-den3d_x0z.txt')
    !OPEN(33, FILE = 'real3d-th-den3d_xy0.txt')
    !CALL WRITE_2DXZ(32, CP2)
    !CALL WRITE_2DXY(33, CP2)
    !CLOSE(32)
    !CLOSE(33)
! 
!  The following line defines a problem which is studied in the time evolution.
  G = GPAR*G
!
  DO K = 1, NRUN	! NRUN iterations to study a nonstationary problem
     T = T + DT
     CALL CALCNU()
     CALL LUX(CP)
     CALL LUY(CP)
     CALL LUZ(CP)
      IF (MOD(K,2).EQ.0) THEN
       !$OMP PARALLEL DO PRIVATE(I, J, KK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
       CALL RAD(CP2, RMS)
       WRITE(8,1008) T*XOP,  RMS(2), RMS(3), RMS(4)
      END IF
  END DO
  CLOSE (8)
  IF(NRUN /= 0)THEN   
    CALL NORM(CP, ZNORM)
    CALL CHEM(CP, MU, EN)
       !$OMP PARALLEL DO PRIVATE(I, J, kK, TMP)
       DO  KK = 0, NZ; DO J = 0, NY;  DO I = 0, NX
         TMP = ABS(CP(I,J,KK))
         CP2(I,J,KK) = TMP * TMP
       END DO; END DO; END DO
       !$OMP END PARALLEL DO
    CALL RAD(CP2, RMS)
    WRITE (17, 1007) ZNORM, MU/XOP, EN/XOP, RMS(1), CP2(NX2, NY2, NZ2)
    WRITE (14, 1017) RMS(1:4)
    1007 FORMAT('After NRUN iter.:',F8.4, 2F12.3, 2F11.3)
    1017 FORMAT (2X, 'After NRUN iter.:', F12.5, 3F13.5)
!
  !  OPEN(31, FILE = 'real3d-th-den-nrun.txt')
  !  CALL WRITE_3D(31, CP2)
  !  CLOSE(31)

   ! OPEN(11, FILE = 'real3d-th-den-nrun1d_z.txt')
   ! OPEN(12, FILE = 'real3d-th-den-nrun1d_x.txt')
   ! OPEN(13, FILE = 'real3d-th-den-nrun1d_y.txt')
   ! CALL DEN1DZ(11, CP2, DEN1Z)
   ! CALL DEN1DX(12, CP2, DEN1X)
   ! CALL DEN1DY(13, CP2, DEN1Y)
   ! CLOSE(11)
   ! CLOSE(12)
   ! CLOSE(13)

    !OPEN(21, FILE = 'real3d-th-den-nrun2d_xz.txt')
    !OPEN(22, FILE = 'real3d-th-den-nrun2d_xy.txt')
    !OPEN(23, FILE = 'real3d-th-den-nrun2d_yz.txt')
    !CALL DEN2DXZ(21, CP2, DEN2XZ)
    !CALL DEN2DXY(22, CP2, DEN2XY)
    !CALL DEN2DYZ(23, CP2, DEN2YZ)
    !CLOSE(21)
    !CLOSE(22)
    !CLOSE(23)

    !OPEN(32, FILE = 'real3d-th-den-nrun3d_x0z.txt')
    !OPEN(33, FILE = 'real3d-th-den-nrun3d_xy0.txt')
    !CALL WRITE_2DXZ(32, CP2)
    !CALL WRITE_2DXY(33, CP2)
    !CLOSE(32)
    !CLOSE(33)
! 
  END IF     
  CALL FREE_VARIABLES()
  IF (ALLOCATED(CP2))    DEALLOCATE(CP2)
! 
  CALL SYSTEM_CLOCK (CLCK_COUNTS_END, CLCK_RATE)
  CALL CPU_TIME(T2)
  WRITE (17, 1001)
  WRITE (14, 1001)
  WRITE (17,*)
  WRITE (17,'(A,I7,A)') ' Clock Time: ', (CLCK_COUNTS_END - CLCK_COUNTS_BEG)/INT (CLCK_RATE,8), ' seconds'
  WRITE (17,'(A,I7,A)') '   CPU Time: ', INT(T2-T1), ' seconds' 
  1008 FORMAT(5F10.4)
!   
END PROGRAM GROSS_PITAEVSKII_SSCN_3D

SUBROUTINE FREE_VARIABLES()
 ! USE UTIL, ONLY : TMPX, TMPY, TMPZ
  USE GPE_DATA, ONLY : X, Y, Z, X2, Y2, Z2, V, CP, R2
  USE CN_DATA, ONLY : CALA, CGAA, CALB, CGAB, CALC, CGAC, CBE
  IMPLICIT NONE
  
  IF (ALLOCATED(CBE)) DEALLOCATE(CBE)
  IF (ALLOCATED(X)) DEALLOCATE(X)   
  IF (ALLOCATED(X2)) DEALLOCATE(X2)   
  IF (ALLOCATED(Y)) DEALLOCATE(Y)   
  IF (ALLOCATED(Y2)) DEALLOCATE(Y2)   
  IF (ALLOCATED(Z)) DEALLOCATE(Z)   
  IF (ALLOCATED(Z2)) DEALLOCATE(Z2)
  IF (ALLOCATED(V)) DEALLOCATE(V)
  IF (ALLOCATED(CP)) DEALLOCATE(CP)
  IF (ALLOCATED(R2)) DEALLOCATE(R2)
 
  IF (ALLOCATED(CALA)) DEALLOCATE(CALA)
  IF (ALLOCATED(CGAA)) DEALLOCATE(CGAA)
  IF (ALLOCATED(CALB)) DEALLOCATE(CALB)
  IF (ALLOCATED(CGAB)) DEALLOCATE(CGAB)
  IF (ALLOCATED(CALC)) DEALLOCATE(CALC)
  IF (ALLOCATED(CGAC)) DEALLOCATE(CGAC)
END SUBROUTINE FREE_VARIABLES
!
SUBROUTINE ALLOCATE_VARIABLES()
!
  USE OMP_LIB
  USE COMM_DATA, ONLY : NX, NY, NZ, NUMBER_OF_THREADS
  USE GPE_DATA, ONLY : X, Y, Z, X2, Y2, Z2, V,  CP, R2
  USE CN_DATA, ONLY : CALA, CGAA, CALB, CGAB, CALC, CGAC, CBE
 
  IMPLICIT NONE
 
  ALLOCATE(CBE(0:MAXVAL([NX,NY,NZ])-1))
!
  ALLOCATE(X(0:NX))   
  ALLOCATE(X2(0:NX))   
  ALLOCATE(Y(0:NY))   
  ALLOCATE(Y2(0:NY))   
  ALLOCATE(Z(0:NZ))   
  ALLOCATE(Z2(0:NZ))
  ALLOCATE(V(0:NX, 0:NY, 0:NZ))
  ALLOCATE(CP(0:NX, 0:NY, 0:NZ))
  ALLOCATE(R2(0:NX, 0:NY, 0:NZ))
  
  ALLOCATE(CALA(0:NX))   
  ALLOCATE(CGAA(0:NX))
  ALLOCATE(CALB(0:NY))   
  ALLOCATE(CGAB(0:NY))
  ALLOCATE(CALC(0:NZ))   
  ALLOCATE(CGAC(0:NZ))
!
END SUBROUTINE ALLOCATE_VARIABLES

SUBROUTINE INITIALIZE()
!   Routine that initializes the constant and variables.
!   Calculates the initial wave function CP
  USE COMM_DATA, ONLY : NX, NY, NZ, NX2, NY2, NZ2, PI, NSTP
  USE GPE_DATA, ONLY : OPTION, DX, DY, DZ, ANU, LAMBDA, GAMMA, X, Y, Z, X2, Y2, Z2, R2, CP
  IMPLICIT NONE
  !REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: TMP3D
  REAL (8) :: SP, PI34, TMP
  INTEGER :: I, J, K
!  
  PI34 = SQRT(PI*SQRT(PI)) ! PI^(3/4)
  SP = PI34 / SQRT(SQRT(ANU * LAMBDA * GAMMA))
!
  FORALL (I=0:NX) X(I) = (I-NX2)*DX
  FORALL (J=0:NY) Y(J) = (J-NY2)*DY
  FORALL (K=0:NZ) Z(K) = (K-NZ2)*DZ
  X2 = X*X
  Y2 = Y*Y
  Z2 = Z*Z
!
  !$OMP PARALLEL DO PRIVATE(I, J, K)
  DO I = 0, NX; DO J = 0, NY;  DO K = 0, NZ
     R2(I,J,K) = X2(I) + Y2(J) + Z2(K)
  END DO; END DO; END DO
  !$OMP END PARALLEL DO
! 
  IF (NSTP == 0) THEN
    WRITE(*,'(a)') "Run the program using the input file to read. e.g.: ./real3d-th < imag3d-den.txt"
    DO I = 0, NX; DO J = 0, NY; DO K = 0, NZ
       READ(*,*) tmp  !READ OUTPUT IMAGTIME PROGRAM WITH SAME PARAMETERS 
       CP(I,J,K) = SQRT(tmp) 
    END DO; END DO; END DO
  ELSE
    !$OMP PARALLEL DO PRIVATE(TMP, I, J, K)
    DO I = 0, NX; DO J = 0, NY; DO K = 0, NZ
       TMP =  (GAMMA*X2(I) + ANU * Y2(J) + LAMBDA * Z2(K))
       CP(I,J,K) = EXP(-TMP/2.0D0)/SP
    END DO; END DO; END DO
    !$OMP END PARALLEL DO
  END IF 
END SUBROUTINE INITIALIZE

SUBROUTINE CALCULATE_TRAP()
  USE COMM_DATA, ONLY : NX, NY, NZ
  USE GPE_DATA, ONLY : XOP, GAMMA, ANU, LAMBDA, V, X2, Y2, Z2
  IMPLICIT NONE
  INTEGER :: I, J, K
  REAL (8) :: ANU2, LAM2, GAM2
  GAM2 = GAMMA * GAMMA
  ANU2 = ANU * ANU
  LAM2 = LAMBDA * LAMBDA
  !$OMP PARALLEL DO PRIVATE(I, J, K)
  DO I = 0, NX; DO J = 0, NY; DO K = 0, NZ
     V(I,J,K) = XOP *  (X2(I)*GAM2 + ANU2*Y2(J) + LAM2*Z2(K))/2.0D0
  END DO; END DO; END DO
  !$OMP END PARALLEL DO
END SUBROUTINE CALCULATE_TRAP

SUBROUTINE COEF()
!  Calculates the coefficients needed in subroutines LUX, LUY, LUZ.    
  USE COMM_DATA, ONLY : NXX, NYY, NZZ  
  USE GPE_DATA, ONLY : DX, DY, DZ, DT, CI
  USE CN_DATA
  IMPLICIT NONE
  INTEGER :: I, J, K
  REAL (8) :: DX2, DY2, DZ2
  REAL (8) :: DXX, DYY, DZZ
  COMPLEX (8) :: CDT
!
  DX2 = DX*DX   ! generating the coefficients for the
  DY2 = DY*DY   ! c-n method (to solve the spatial part)
  DZ2 = DZ*DZ
!     
  DXX = 1.D0/DX2
  DYY = 1.D0/DY2
  DZZ = 1.D0/DZ2
  CDT = CI*DT
!
  CA0 = 1.D0 + CDT*DXX
  CA0R = 1.D0 - CDT*DXX
  CB0 = 1.D0 + CDT*DYY
  CB0R = 1.D0 - CDT*DYY
  CC0 = 1.D0 + CDT*DZZ
  CC0R = 1.D0 - CDT*DZZ
!
  CT0X = -CDT*DXX/2.D0
  CALA(NXX) = 0.D0
  CGAA(NXX) = -1.D0/CA0
  DO I = NXX, 1, -1
     CALA(I-1) = CT0X*CGAA(I)
     CGAA(I-1) = -1.D0/(CA0+CT0X*CALA(I-1))
  END DO
!
  CT0Y = -CDT*DYY/2.D0
  CALB(NYY) = 0.D0
  CGAB(NYY) = -1.D0/CB0
  DO J = NYY, 1, -1
     CALB(J-1) = CT0Y*CGAB(J)
     CGAB(J-1) = -1.D0/(CB0+CT0Y*CALB(J-1))
  END DO
!
  CT0Z = -CDT*DZZ/2.D0
  CALC(NZZ) = 0.D0
  CGAC(NZZ) = -1.D0/CC0
  DO K = NZZ, 1, -1
     CALC(K-1) = CT0Z*CGAC(K)
     CGAC(K-1) = -1.D0/(CC0+CT0Z*CALC(K-1))
  END DO
END SUBROUTINE COEF

SUBROUTINE CALCNU() ! Exact solution
!  Solves the partial differential equation with the potential and the
!  nonlinear term.      
  USE OMP_LIB
  USE COMM_DATA, ONLY : NX, NY, NZ
  USE GPE_DATA, ONLY : V, G, CI,CP,DT
  IMPLICIT NONE 
! 
   REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) ::   P2, TMP
  INTEGER :: I, J, K
!
  !$OMP PARALLEL DO PRIVATE(I, J, K)
   DO K = 0, NZ; DO J = 0, NY; DO I = 0, NX
   !   P(I,J,K) = ABS(CP(I,J,K))
      P2(I,J,K) =             ABS(CP(I,J,K))**2                 !P(I,J,K) *  P(I,J,K) 
      TMP(I,J,K) = DT*(V(I,J,K) + G*P2(I,J,K))
      CP(I,J,K) = CP(I,J,K)*EXP(-CI*TMP(I,J,K))
   END DO; END DO; END DO
  !$OMP END PARALLEL DO
END SUBROUTINE CALCNU

SUBROUTINE LUX(CP)
!  Solves the partial differential equation only with the X-space
!  derivative term using the Crank-Nicholson method 
  USE COMM_DATA
 !  USE GPE_DATA, ONLY : CP
  USE CN_DATA, ONLY : CA0R, CT0X, CALA, CGAA , CBE
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:,0:), INTENT(INOUT) :: CP
   COMPLEX (8), DIMENSION(0:NX,0:NY,0:NZ) :: TMP
!
!  COMPLEX (8) :: CXX
  INTEGER :: I, J, K
!
  !$OMP PARALLEL DO PRIVATE(I,J, K, TMP, CBE)
  DO J = 0, NY; DO K = 0, NZ
    CBE(NXX) = CP(NXX,J,K)
    DO I = NXX, 1, -1
     TMP(I,J,K) = -CT0X*CP(I+1,J,K)+CA0R*CP(I,J,K)-CT0X*CP(I-1,J,K)
      CBE(I-1) = CGAA(I)*(CT0X*CBE(I)-TMP(I,J,K))
    END DO
!-----------------------------
! Boundary condition periodic:
!    DO I = 0, NXX
!      CP(I+1,J,K) = CALA(I)*CP(I,J,K) + CBE(I,J,K)
!    END DO
!    CP(0,J,K) = CP(NX,J,K)
!    CP(1,J,K) = CP(NXX,J,K)
!-----------------------------
! Boundary condition reflecting:
    CP(0,J,K) = 0.0D0
    DO I = 0, NXX
      CP(I+1,J,K) = CALA(I)*CP(I,J,K) + CBE(I)
    END DO
    CP(NX,J,K) = 0.0D0
  END DO; END DO
  !$OMP END PARALLEL DO
END SUBROUTINE LUX

SUBROUTINE LUY(CP)
!  Solves the partial differential equation only with the Y-space
!  derivative term using the Crank-Nicholson method
  USE COMM_DATA
! USE GPE_DATA, ONLY : CP
  USE CN_DATA, ONLY : CB0R, CT0Y, CALB, CGAB, CBE
  USE OMP_LIB
  IMPLICIT NONE 
 COMPLEX (8), DIMENSION(0:,0:,0:), INTENT(INOUT) :: CP
 COMPLEX (8), DIMENSION(0:NX,0:NY,0:NZ) :: TMP

!  COMPLEX (8) :: CYY
  INTEGER :: I, J, K
!
  !$OMP PARALLEL DO PRIVATE(I, J,K, TMP, CBE) 
  DO K = 0, NZ; DO I = 0, NX 
    CBE(NYY) = CP(I,NYY,K)
    DO J = NYY, 1, -1
       TMP(I,J,K) = -CT0Y*CP(I,J+1,K)+CB0R*CP(I,J,K)-CT0Y*CP(I,J-1,K)
        CBE(J-1) = CGAB(J)*(CT0Y*CBE(J)-TMP(I,J,K))
    END DO
!-----------------------------
! Boundary condition periodic:
!    DO J = 0, NYY
!      CP(I,J+1,K) = CALB(J)*CP(I,J,K) + CBE(I,J,K)
!    END DO
!    CP(I,0,K) = CP(I,NY,K)
!    CP(I,1,K) = CP(I,NYY,K)
!-----------------------------
! Boundary condition reflecting:
    CP(I,0,K) = 0.0D0
    DO J = 0, NYY
      CP(I,J+1,K) = CALB(J)*CP(I,J,K) + CBE(J)
    END DO
    CP(I,NY,K) = 0.0D0
  END DO; END DO
  !$OMP END PARALLEL DO
!
END SUBROUTINE LUY

SUBROUTINE LUZ(CP)
!  Solves the partial differential equation only with the Y-space
!  derivative term using the Crank-Nicholson method
  USE COMM_DATA
! USE GPE_DATA, ONLY : CP
  USE CN_DATA, ONLY :   CC0R, CT0Z, CALC, CGAC, CBE
  USE OMP_LIB
  IMPLICIT NONE 
  COMPLEX (8), DIMENSION(0:,0:,0:), INTENT(INOUT) :: CP
  COMPLEX (8), DIMENSION(0:NX,0:NY,0:NZ) :: TMP
 ! COMPLEX (8) :: CZZ
  INTEGER :: I, J, K


  !$OMP PARALLEL DO PRIVATE(I, J,K, TMP, CBE)
  DO J = 0, NY; DO I = 0, NX 
    CBE(NZZ) = CP(I,J,NZZ)
    DO K = NZZ, 1, -1
       TMP(I,J,K) = -CT0Z*CP(I,J,K+1)+CC0R*CP(I,J,K)-CT0Z*CP(I,J,K-1)
       CBE(K-1) = CGAC(K)*(CT0Z*CBE(K)-TMP(I,J,K))
    END DO
!-----------------------------
! Boundary condition periodic:
!    DO K = 0, NZZ
!      CP(I,J,K+1) = CALC(K)*CP(I,J,K) + CBE(K)
!    END DO
!    CP(I,J,0) = CP(I,J,NZ)
!    CP(I,J,1) = CP(I,J,NZZ)
!-----------------------------
! Boundary condition reflecting:
    CP(I,J,0) = 0.0D0
    DO K = 0, NZZ
      CP(I,J,K+1) = CALC(K)*CP(I,J,K) + CBE(K)
    END DO
    CP(I,J,NZ) = 0.0D0
  END DO; END DO
  !$OMP END PARALLEL DO
!
END SUBROUTINE LUZ

SUBROUTINE NORM(CP, ZNORM)
! Calculates the normalization of the wave function and sets it to unity.
  USE COMM_DATA, ONLY : NX, NY, NZ
  USE GPE_DATA, ONLY : DX, DY, DZ
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:,0:), INTENT(INOUT) :: CP
  REAL (8), INTENT(OUT) :: ZNORM
!-------------------------------------------------
  INTERFACE
    FUNCTION INTEGRATE(U, DX, DY, DZ) RESULT(VALUE)
      REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U
      REAL (8), INTENT (IN) :: DX, DY, DZ
      REAL (8) :: VALUE
    END FUNCTION INTEGRATE
  END INTERFACE
!-------------------------------------------------
  REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: P2
!
  P2 = ABS(CP)**2
  ZNORM = SQRT(INTEGRATE(P2, DX, DY, DZ))
!  CP = CP/ZNORM  
END SUBROUTINE NORM

SUBROUTINE RAD(CP2, R)
! Calculates the root mean square size RMS
  USE COMM_DATA, ONLY : NX, NY, NZ
  USE GPE_DATA, ONLY : DX, DY, DZ, X2, Y2, Z2, R2
  IMPLICIT NONE
  REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: CP2
  REAL (8), DIMENSION(:), INTENT(OUT) :: R
!-------------------------------------------------
  INTERFACE
    FUNCTION INTEGRATE(U, DX, DY, DZ) RESULT(VALUE)
      REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U
      REAL (8), INTENT (IN) :: DX, DY, DZ
      REAL (8) :: VALUE
    END FUNCTION INTEGRATE
  END INTERFACE
!-------------------------------------------------
  INTEGER :: I, J, K
  REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: TMP3D
!
  TMP3D = R2*CP2
   R(1) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))
  FORALL(J=0:NY, K=0:NZ) TMP3D(:,J,K) = X2*CP2(:,J,K)
   R(2) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))
  FORALL(I=0:NX, K=0:NZ) TMP3D(I,:,K) = Y2*CP2(I,:,K)
   R(3) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))
  FORALL(I=0:NX, J=0:NY) TMP3D(I,J,:) = Z2*CP2(I,J,:)
   R(4) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))
END SUBROUTINE RAD

 SUBROUTINE CHEM(CP, MU, EN)
!  Calculates the chemical potential MU and energy EN.  CP is the wave
!  function, V is the potential and G is the nonlinearity.
   USE COMM_DATA, ONLY : NX, NY, NZ, NXX, NYY, NZZ
   USE GPE_DATA, ONLY : DX, DY, DZ,   V, G 
   IMPLICIT NONE
   COMPLEX (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: CP
   REAL (8), INTENT(OUT) :: MU, EN
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION DIFF(P, DX) RESULT (DP)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: P
      REAL (8), INTENT(IN) :: DX
      REAL (8), DIMENSION(0:SIZE(P)-1) :: DP
    END FUNCTION DIFF
  END INTERFACE
!-------------------------------------------------
  INTERFACE
    FUNCTION INTEGRATE(U, DX, DY, DZ) RESULT(VALUE)
      REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U
      REAL (8), INTENT (IN) :: DX, DY, DZ
      REAL (8) :: VALUE
    END FUNCTION INTEGRATE
  END INTERFACE
!-------------------------------------------------
   INTEGER :: I, J, K
   REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: DP, DPX, DPY, DPZ
   REAL (8), DIMENSION(0:NX, 0:NY, 0:NZ) :: P, P2, GP2,  DP2, TMP3D,EMP3D
!
   P = ABS(CP)
 !  DO I = 0, NX; DO J = 0,NY
 !     DPZ(I,J,0:NZ) = DIFF(P(I,J,0:NZ), DZ)
 !  END DO; END DO
 !  DO I = 0, NX; DO K = 0,NZ
 !     DPY(I,0:NY,K) = DIFF(P(I,0:NY,K), DY)
!   END DO; END DO
!   DO J = 0, NY; DO K = 0,NZ
!      DPX(0:NX,J,K) = DIFF(P(0:NX,J,K), DX)
!   END DO; END DO

 !$OMP PARALLEL
   !$OMP DO PRIVATE(I,J)
   DO J = 0, NY; DO I = 0, NX
      DPZ(I,J,:) = DIFF(P(I,J,:), DZ)
   END DO; END DO
   !$OMP END DO

   !$OMP DO PRIVATE(I,K)
   DO K = 0, NZ; DO I = 0, NX
      DPY(I,:,K) = DIFF(P(I,:,K), DY)
   END DO; END DO
   !$OMP END DO

   !$OMP DO PRIVATE(J,K)
   DO J = 0, NY; DO K = 0, NZ
      DPX(:,J,K) = DIFF(P(:,J,K), DX)
   END DO; END DO
   !$OMP END DO
   !$OMP END PARALLEL

   !$OMP PARALLEL 
   !$OMP DO PRIVATE(I,J,K)
   DO K = 0, NZ; DO J = 0, NY; DO I = 0, NX
     P2(I,J,K) = P(I,J,K) * P(I,J,K)
     GP2(I,J,K) = G * P2(I,J,K)
     DP2(I,J,K) = DPX(I,J,K)*DPX(I,J,K) + DPY(I,J,K)*DPY(I,J,K) + DPZ(I,J,K)*DPZ(I,J,K)
     TMP3D(I,J,K) = (V(I,J,K) + GP2(I,J,K) ) * P2(I,J,K) + DP2(I,J,K)
     EMP3D(I,J,K) = (V(I,J,K) + GP2(I,J,K)/2.0D0) * P2(I,J,K) + DP2(I,J,K)
   END DO; END DO; END DO
   !$OMP END DO
   !$OMP END PARALLEL


!
!   P2 = P*P 
!   GP2 = G*P2
!   DP2 = DPX*DPX + DPY*DPY + DPZ*DPZ
!      
!   TMP3D = (V + GP2)*P2 + DP2
!   EMP3D = (V + GP2/2.D0)*P2 + DP2 
!   
   MU = INTEGRATE(TMP3D, DX, DY, DZ)
   EN = INTEGRATE(EMP3D, DX, DY, DZ)
END SUBROUTINE CHEM

FUNCTION INTEGRATE(U, DX, DY, DZ) RESULT(RES)
  USE COMM_DATA, ONLY : NX, NY, NZ
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U
  REAL (8), INTENT (IN) :: DX, DY, DZ
  REAL (8) :: RES
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
 
   REAL (8), DIMENSION(0:NX) :: TMPX
 REAL (8), DIMENSION(0:NY) :: TMPY
   REAL (8), DIMENSION(0:NZ) :: TMPZ
  INTEGER :: I, J,K
   !$OMP PARALLEL DO PRIVATE(I, J, TMPX, TMPY)
  DO K = 0, NZ
    DO J = 0, NY
      DO I = 0, NX      
        TMPX(I) = U(I,J,K)
      END DO
      TMPY(J) = SIMP(TMPX, DX)
    END DO
    TMPZ(K) = SIMP(TMPY, DY)
  END DO
  !$OMP END PARALLEL DO
  RES = SIMP(TMPZ, DZ)

! 
END FUNCTION INTEGRATE

PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
! Does the spatial integration with Simpson's rule.
! N refer to the number of integration points, DX space step, and
! F is the function to be integrated.
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(IN) :: F
  REAL (8), INTENT(IN) :: DX
  REAL (8) :: VALUE
  REAL (8) :: F1, F2
  INTEGER :: I, N
!
  N = SIZE(F) - 1
  F1 = F(1) + F(N-1) ! N EVEN
  F2 = F(2) 
  DO I = 3, N-3, 2
     F1 = F1 + F(I)
     F2 = F2 + F(I+1)
  END DO
  VALUE = DX*(F(0) + 4.D0*F1 + 2.D0*F2 + F(N))/3.D0
END FUNCTION SIMP

PURE FUNCTION DIFF(P,DX) RESULT (DP)
! Computes the first derivative DP of P using
! Richardsonextrapolation formula. The derivative at the  
! boundaries are assumed to be zero
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(IN) :: P
  REAL (8), INTENT(IN) :: DX
  REAL (8), DIMENSION(0:SIZE(P)-1) :: DP
  INTEGER :: I, N
!
  N = SIZE(P) - 1
  DP(0) = 0.D0
  DP(1) = (P(2) - P(0))/(2.D0*DX)
  FORALL(I=2:N-2)
    DP(I) = (P(I-2)-8.D0*P(I-1)+8.D0*P(I+1)-P(I+2))/(12.D0*DX)
  END FORALL
  DP(N-1) = (P(N) - P(N-2))/(2.D0*DX)
  DP(N) = 0.D0
END FUNCTION DIFF

SUBROUTINE WRITE_3D(FUNIT, U2)
  USE COMM_DATA, ONLY : NX, NY, NZ
  INTEGER :: FUNIT
  REAL (8), DIMENSION(0:,0:,0:) :: U2
  INTEGER :: I, J, K
!
  DO I = 0, NX
     DO J = 0, NY
        DO K = 0, NZ
           WRITE(FUNIT, 1000) U2(I,J,K)
        END DO
     END DO
  END DO
   1000 FORMAT(E17.5E3)
END SUBROUTINE WRITE_3D
 
SUBROUTINE WRITE_2DXY(FUNIT, U2)
  USE COMM_DATA, ONLY : NX, NY, NZ2
  USE GPE_DATA, ONLY : X, Y
  INTEGER :: FUNIT
  REAL (8), DIMENSION(0:,0:,0:) :: U2
  INTEGER :: I, J, K
!
  K = NZ2
  DO I = 0, NX
     DO J = 0, NY
        WRITE(FUNIT, 1000) X(I), Y(J), U2(I,J,K)
     END DO
     WRITE(FUNIT, *)
  END DO 
  1000 FORMAT(2F10.2, E17.5E3)
END SUBROUTINE WRITE_2DXY

SUBROUTINE WRITE_2DXZ(FUNIT, U2)
  USE COMM_DATA, ONLY : NX, NZ, NY2 
  USE GPE_DATA, ONLY : X, Z
  INTEGER :: FUNIT
  REAL (8), DIMENSION(0:,0:,0:) :: U2
  INTEGER :: I, J, K
!
  J = NY2
  DO I = 0, NX
     DO K = 0, NZ
        WRITE(FUNIT, 1000) X(I), Z(K), U2(I,J,K)
     END DO
     WRITE(FUNIT, *)
  END DO
  1000 FORMAT(2F10.2, E17.5E3)
END SUBROUTINE WRITE_2DXZ

SUBROUTINE DEN2DXY(FUNIT, U2, DE2DXY)
  USE COMM_DATA, ONLY : NX, NY  
  USE GPE_DATA, ONLY : X,  DZ, Y
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U2
  REAL (8), DIMENSION(0:, 0:), INTENT(OUT) :: DE2DXY
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
  INTEGER :: I, J, FUNIT
!
  FORALL (I = 0:NX, J = 0:NY) DE2DXY(I,J) = SIMP(U2(I,J,0:), DZ)
  DO I = 0, NX
   DO J = 0,NY
     WRITE(FUNIT, 1000) X(I),Y(J), DE2DXY(I,J)
   END DO
    WRITE(FUNIT, 1000)
  END DO
  1000 FORMAT(2F10.2, E17.5E3)
END SUBROUTINE DEN2DXY

SUBROUTINE DEN2DXZ(FUNIT, U2, DE2DXZ)
  USE COMM_DATA, ONLY : NX, NZ  
  USE GPE_DATA, ONLY : X, DY, Z
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U2
  REAL (8), DIMENSION(0:, 0:), INTENT(OUT) :: DE2DXZ
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
  INTEGER :: I, K, FUNIT
!
  FORALL (I = 0:NX, K = 0:NZ) DE2DXZ(I,K) = SIMP(U2(I,0:,K), DY)
  DO I = 0, NX
   DO K = 0,NZ
     WRITE(FUNIT, 1000) X(I),Z(K), DE2DXZ(I,K)
   END DO
    WRITE(FUNIT, 1000)
  END DO
  1000 FORMAT(2F10.2,E17.5E3)
END SUBROUTINE DEN2DXZ

SUBROUTINE DEN2DYZ(FUNIT, U2, DE2DYZ)
  USE COMM_DATA, ONLY : NY, NZ  
  USE GPE_DATA, ONLY : Y, DX, Z
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U2
  REAL (8), DIMENSION(0:, 0:), INTENT(OUT) :: DE2DYZ
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
  INTEGER :: J, K, FUNIT
!
  FORALL (J = 0:NY, K = 0:NZ) DE2DYZ(J,K) = SIMP(U2(0:,J,K), DX)
  DO J = 0, NY
    DO K = 0,NZ
      WRITE(FUNIT, 1000) Y(J),Z(K), DE2DYZ(J,K)
    END DO
     WRITE(FUNIT, 1000)
  END DO
  1000 FORMAT(2F10.2, E17.5E3)
END SUBROUTINE DEN2DYZ

SUBROUTINE DEN1DX(FUNIT, U2, DE1DX)
  USE COMM_DATA, ONLY : NX, NZ
  USE GPE_DATA, ONLY : X, DY, DZ 
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U2
  REAL (8), DIMENSION(0:), INTENT(OUT) :: DE1DX
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
  REAL (8), DIMENSION(0:NX, 0:NZ) :: TMP2D
  INTEGER :: I, K, FUNIT
!
  FORALL (I = 0:NX, K = 0:NZ) TMP2D(I,K) = SIMP(U2(I,0:,K), DY)
      DO I = 0, NX 
	 DE1DX(I) = SIMP(TMP2D(I,0:), DZ)
         WRITE(FUNIT, 1001) X(I), DE1DX(I)
      END DO
  1001 FORMAT(F10.2, E17.5E3)
END SUBROUTINE DEN1DX
 
SUBROUTINE DEN1DY(FUNIT, U2, DE1DY)
  USE COMM_DATA, ONLY : NY, NZ
  USE GPE_DATA, ONLY : Y, DX, DZ  
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U2
  REAL (8), DIMENSION(0:), INTENT(OUT) :: DE1DY
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
  REAL (8), DIMENSION(0:NY, 0:NZ) :: TMP2D
  INTEGER :: J, K, FUNIT
!
  FORALL (J = 0:NY, K = 0:NZ) TMP2D(J,K) = SIMP(U2(0:,J,K), DX)
      DO J = 0, NY 
	 DE1DY(J) = SIMP(TMP2D(J,0:), DZ)
         WRITE(FUNIT, 1001) Y(J), DE1DY(J)
      END DO
  1001 FORMAT(F10.2, E17.5E3)
END SUBROUTINE DEN1DY

SUBROUTINE DEN1DZ(FUNIT, U2, DE1DZ)
  USE COMM_DATA, ONLY : NY, NZ
  USE GPE_DATA, ONLY : Z, DX, DY   
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U2
  REAL (8), DIMENSION(0:), INTENT(OUT) :: DE1DZ
!-------------------------------------------------
  INTERFACE
    PURE FUNCTION SIMP(F, DX) RESULT (VALUE)
      IMPLICIT NONE
      REAL (8), DIMENSION(0:), INTENT(IN) :: F
      REAL (8), INTENT(IN) :: DX
      REAL (8) :: VALUE
    END FUNCTION SIMP
  END INTERFACE
!-------------------------------------------------
  REAL (8), DIMENSION(0:NY, 0:NZ) :: TMP2D
  INTEGER :: J, K, FUNIT
!
  FORALL (J = 0:NY, K = 0:NZ) TMP2D(J,K) = SIMP(U2(0:,J,K), DX)
      DO K = 0, NZ 
	 DE1DZ(K) = SIMP(TMP2D(0:,K), DY)
         WRITE(FUNIT, 1001) Z(K), DE1DZ(K)
      END DO
  1001 FORMAT(F10.2, E17.5E3)
END SUBROUTINE DEN1DZ

!# File name : real3d-th.f90
